//*********************************************
// Required by build script to generate tutorial
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath group: 'org.asciidoctor', name: 'asciidoctor-gradle-plugin', version: '1.5.3'
    classpath group: 'org.asciidoctor', name: 'asciidoctorj-pdf', version: '1.5.0-alpha.14'
  }
}
apply plugin: "org.asciidoctor.convert"
apply plugin: "java"
apply plugin: "war"
apply plugin: 'idea'
apply plugin: 'eclipse-wtp'

//*********************************************
// Required to build a BCD-UI webapp
dependencies {
  repositories {
    mavenCentral()
    maven {
      url "https://github.com/businesscode/maven-repo/raw/master"
    }
    compileOnly group: 'javax.servlet', name: 'servlet-api', version: '2.4'
    compile 'de.businesscode.bcdui:bcd-ui-core:5.0.1-SNAPSHOT'
    runtime 'de.businesscode.bcdui:bcd-ui-theme-bcd:5.0.1-SNAPSHOT'
  }
}

//*********************************************
// Settings for creating a war
webAppDirName = "web"
war {
  baseName   = "bcduidocu"
  includeEmptyDirs = false
}


//*********************************************
// Generate tutorial HTML from all.adoc
task buildTutorial ( type: org.asciidoctor.gradle.AsciidoctorTask ) {

  ext.taskDestDirName = "$buildDir/htmldocs/tutorial"
  def taskSource      = "tutorial"

  // Resources under ./images/ are per default copied by AsciidoctorTask
  configure {
    sources {
      include "index.adoc"
    }
    backends = ['html5'] // ['html5','pdf']
    separateOutputDirs = false
    attributes 'source-highlighter' : 'coderay',
            'coderay-linenums-mode' : 'table',
            icons                   : 'font',
            linkattrs               : true,
            encoding                : 'UTF-8'
    sourceDir = file(taskSource)
    outputDir = file(taskDestDirName)
  }
}

//*********************************************
// This collects BCD-UI docu (tutorial, js-api, java-api) to /docs
// This task requires BCD-UI cloned next to BCD-UI-Docu and task bcduiBuildDocu being executed there already
// GitHub pages is setup to use /docs
task collectDocu( type: Copy ) {
  group 'bcdui'
  dependsOn buildTutorial

  def bcduiDocuFolder = '../BCD-UI/Docu/build/htmldocs'

  if( ! file(bcduiDocuFolder).exists() )
    logger.warn("$bcduiDocuFolder not found, only collecting tutorial")

  into 'docs' // This is a GitHub pages friendly location

  // From BCD-UI-Tutorial project
  from( buildTutorial.taskDestDirName ) {
    into "tutorial"
  }

  // From BCD-UI project
  from( bcduiDocuFolder )
  from( '../BCD-UI/Docu/xsd' ) {
    into 'xsd'
  }
}



//*********************************************
// For Eclipse WTP, we set the context path
// Will then usually be http://localhost:8080/BCD-UI-Docu
eclipse {
  wtp {
    component {
      contextPath = 'BCD-UI-Docu'
    }
  }
}

//*********************************************
// For Intellij IDEA
// Will then usually be http://localhost:8080/BCD-UI-Docu
idea {
  module {
    jdkName = '1.8'
  }
}
