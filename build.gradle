//*********************************************
// Required by build script to generate tutorial
buildscript {
  repositories {
  }
  dependencies {
  }
}
plugins {
  id 'org.asciidoctor.jvm.convert' version '4.0.4'
}

apply plugin: "java"
apply plugin: "war"
apply plugin: 'idea'
apply plugin: 'eclipse-wtp'
import org.asciidoctor.gradle.jvm.AsciidoctorTask

//*********************************************
// Required to build a BCD-UI webapp
dependencies {
  repositories {
    mavenCentral()
    maven {
      url "https://github.com/businesscode/maven-repo/raw/master"
    }
  }
  
  // BCD-UI
  // For list of loaded dependencies, check Docu/development/bcdui_dependencies.gradle
  // at https://github.com/businesscode/BCD-UI/
  // Or pom at https://github.com/businesscode/maven-repo
  implementation 'de.businesscode.bcdui:bcd-ui-core:5.7.0-SNAPSHOT'
  implementation 'de.businesscode.bcdui:bcd-ui-theme-bcd:5.7.0-SNAPSHOT'

  // 3rd party
  implementation group: 'org.duckdb', name: 'duckdb_jdbc', version: '1.4.3.0'
}

//*********************************************
// Settings for creating a war
webAppDirName = "web"
war {
  archiveBaseName   = "bcduidocu"
  includeEmptyDirs = false
}


//*********************************************
// Generate tutorial HTML from all.adoc
task buildTutorial(type: AsciidoctorTask) {

  ext.taskDestDirName = "$buildDir/htmldocs/tutorial"
  def taskSource      = "tutorial"
  // Resources under ./images/ are per default copied by AsciidoctorTask
  sources {
    include "index.adoc"
  }
  baseDirFollowsSourceDir()
  configure {
    attributes 'source-highlighter' : 'coderay',
            'coderay-linenums-mode' : 'table',
            icons                   : 'font',
            linkattrs               : true,
            encoding                : 'UTF-8'
    outputDir = file(taskDestDirName)
    sourceDir = file(taskSource)

  }
  outputOptions {
    backends = ['html5'] // ['html5','pdf']
    separateOutputDirs = false
  }
}

/**
 * Collect js Api subs and TypeScript .d.ts type files
 */
task zipJsApiStubs(type: Zip) {
    from '../BCD-UI/Docu/build/jsApiStubs'
    archiveFileName = "bcduiApiStubs-${getProperty('bcdui.version')}.zip"
    destinationDirectory = file('resources')
}
task zipTypescriptApi(type: Zip) {
    from '../BCD-UI/Docu/build/tsTypes'
    archiveFileName = "bcduiTsTypes-${getProperty('bcdui.version')}.zip"
    destinationDirectory = file('resources')
}

//*********************************************
// This collects BCD-UI docu (tutorial, js-api, java-api) to /docs
// The resulting structure is aligned with GitHub
// This task requires BCD-UI cloned next to BCD-UI-Docu and task bcduiBuildDocu being executed there already
// GitHub pages is setup to use /docs
task updateDocs( type: Copy ) {
  group 'bcdui'
  dependsOn buildTutorial, zipJsApiStubs, zipTypescriptApi

  def bcduiDocuFolder = '../BCD-UI/Docu/build/htmldocs'

  doFirst {
    delete 'docs'
  }

  if( ! file(bcduiDocuFolder).exists() )
    logger.warn("$bcduiDocuFolder not found (execute bcduiBuildDocu in BCD-UI), only collecting tutorial")

  into 'docs' // This is a GitHub pages friendly location

  // From BCD-UI-Tutorial project
  from( buildTutorial.taskDestDirName ) {
    into "tutorial"
  }

  // From BCD-UI project
  from( bcduiDocuFolder )
  from( '../BCD-UI/Docu/xsd' ) {
    into 'xsd'
  }


  // Replace version and date placeholders
  doLast {
    fileTree( dir: "docs" ).each { f ->
      String content = f.getText("UTF-8")
      if( content.contains("--bcd-dyn-replace-current-version-date--") ) {
        content = content.replaceAll('--bcd-dyn-replace-current-version-date--', (getProperty('bcdui.version') + " (" + (String) new Date().format('yyyy-MM-dd'))+")")
        f.write(content, "UTF-8")
      }
    }
  }

}



//*********************************************
// For Eclipse WTP, we set the context path
// Will then usually be http://localhost:8080/BCD-UI-Docu
eclipse {
  wtp {
    component {
      contextPath = 'BCD-UI-Docu'
    }
  }
}

//*********************************************
// For Intellij IDEA
// Will then usually be http://localhost:8080/BCD-UI-Docu
idea {
  module {
    jdkName = '17'
  }
}
